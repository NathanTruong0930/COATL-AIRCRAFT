#include <SPI.h>
#include <WiFi.h>
#include <WebServer.h>

// === WiFi Configuration ===
const char* ssid = "EA";
const char* password = "123456Ee";

// === Web server on port 80 ===
WebServer server(80);

// ADG212 control pins
#define ROUTE1_PIN 18  // 39kΩ
#define ROUTE2_PIN 17  // 150kΩ
#define ROUTE3_PIN 9   // 470kΩ

// MAX1032 SPI configuration
#define CS_PIN     8
#define SPI_SCK    36
#define SPI_MOSI   35
#define SPI_MISO   37

// ADC settings
#define V_REF             4.098
#define ADC_RANGE_BYTE    0b10000100
#define MODE_CONTROL_BYTE 0b10000000

// ADC switching thresholds
#define R3_MIN 7738
#define R3_MAX 8645

#define R2_MIN_1 6770
#define R2_MAX_1 7737
#define R2_MIN_2 8646
#define R2_MAX_2 9613

#define R1_MIN_1 0
#define R1_MAX_1 6769
#define R1_MIN_2 9614
#define R1_MAX_2 16383

// Data tracking
int currentRoute = 1;
unsigned long lastADCRead = 0;
uint16_t adcRaw = 0;
float voltage = 0.0;

// === Serial Monitor Web Log ===
constexpr uint16_t LOG_DEPTH = 120;
String logBuf[LOG_DEPTH];
uint16_t logHead = 0;

void addLogLine(const String& ln) {
  logBuf[logHead] = ln;
  logHead = (logHead + 1) % LOG_DEPTH;
}

void handleLog() {
  String out;
  for (uint16_t i = 0; i < LOG_DEPTH; ++i) {
    uint16_t idx = (logHead + i) % LOG_DEPTH;
    if (logBuf[idx].length()) out += logBuf[idx] + "\n";
  }
  server.send(200, "text/plain", out);
}

void handleRoot() {
  String html =
    "<html><head><title>EDA Monitor</title>"
    "<style>body{font-family:sans-serif;margin:20px}"
    "pre{background:#000;color:#0f0;padding:8px;height:300px;"
    "overflow-y:scroll;font-family:monospace;}"
    "</style>"
    "<script>"
    "function upd(){fetch('/log').then(r=>r.text()).then(t=>{"
    "let p=document.getElementById('log'); p.textContent=t;"
    "});}setInterval(upd,1000);window.onload=upd;"
    "</script></head><body>"
    "<h2>Electrostatic Dust Analyzer Status</h2>"
    "<p><b>Active Route:</b> " + String(currentRoute) + "</p>"
    "<p><b>ADC Raw:</b> " + String(adcRaw) + "</p>"
    "<p><b>Voltage:</b> " + String(voltage, 4) + " V</p>"
    "<h3>Serial Monitor</h3><pre id='log'></pre>"
    "</body></html>";
  server.send(200, "text/html", html);
}

void setup() {
  Serial.begin(115200);

  pinMode(ROUTE1_PIN, OUTPUT);
  pinMode(ROUTE2_PIN, OUTPUT);
  pinMode(ROUTE3_PIN, OUTPUT);
  disableAllRoutes();
  enableRoute(1);

  pinMode(CS_PIN, OUTPUT);
  digitalWrite(CS_PIN, HIGH);
  SPI.begin(SPI_SCK, SPI_MISO, SPI_MOSI);
  SPI.beginTransaction(SPISettings(500000, MSBFIRST, SPI_MODE0));
  configureADC();

  WiFi.begin(ssid, password);
  Serial.print("Connecting to WiFi");
  while (WiFi.status() != WL_CONNECTED) {
    delay(500); Serial.print(".");
  }
  Serial.println("\nWiFi connected, IP address: " + WiFi.localIP().toString());
  addLogLine("WiFi connected: " + WiFi.localIP().toString());

  server.on("/", handleRoot);
  server.on("/log", handleLog);
  server.begin();
  Serial.println("Web server started");
  addLogLine("Web server started");
}

void loop() {
  server.handleClient();

  // Read ADC every 1 second
  if (millis() - lastADCRead >= 1000) {
    adcRaw = readMAX1032();
    voltage = ((adcRaw / 16383.0) - 0.5) * (3 * V_REF);

    // === Dynamic Route Switching Based on adcRaw ===
    int newRoute = 1; // default
    if (adcRaw >= R3_MIN && adcRaw <= R3_MAX) {
      newRoute = 3;
    } else if ((adcRaw >= R2_MIN_1 && adcRaw <= R2_MAX_1) || (adcRaw >= R2_MIN_2 && adcRaw <= R2_MAX_2)) {
      newRoute = 2;
    }

    if (newRoute != currentRoute) {
      currentRoute = newRoute;
      disableAllRoutes();
      enableRoute(currentRoute);
      String msg = "Auto-switched to ROUTE " + String(currentRoute) + " for ADC: " + String(adcRaw);
      Serial.println(msg);
      addLogLine(msg);
    }

    String adcMsg = "ADC Raw: " + String(adcRaw) +
                    " | Voltage: " + String(voltage, 4) +
                    " V | Active Route: " + String(currentRoute);
    Serial.println(adcMsg);
    addLogLine(adcMsg);

    lastADCRead = millis();
  }
}

void enableRoute(int route) {
  digitalWrite(ROUTE1_PIN, route == 1);
  digitalWrite(ROUTE2_PIN, route == 2);
  digitalWrite(ROUTE3_PIN, route == 3);
}

void disableAllRoutes() {
  digitalWrite(ROUTE1_PIN, LOW);
  digitalWrite(ROUTE2_PIN, LOW);
  digitalWrite(ROUTE3_PIN, LOW);
}

void configureADC() {
  digitalWrite(CS_PIN, LOW);
  SPI.transfer(MODE_CONTROL_BYTE);
  digitalWrite(CS_PIN, HIGH);
  delayMicroseconds(10);

  digitalWrite(CS_PIN, LOW);
  SPI.transfer(ADC_RANGE_BYTE);
  digitalWrite(CS_PIN, HIGH);
  delayMicroseconds(100);
}

uint16_t readMAX1032() {
  uint8_t response[3] = {0};
  digitalWrite(CS_PIN, LOW);
  SPI.transfer(MODE_CONTROL_BYTE);
  response[0] = SPI.transfer(0x00);
  response[1] = SPI.transfer(0x00);
  response[2] = SPI.transfer(0x00);
  digitalWrite(CS_PIN, HIGH);
  return (((response[1] << 8) | response[2]) >> 2) & 0x3FFF;
}
